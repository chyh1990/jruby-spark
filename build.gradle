group 'com.sensetime.test'

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'idea'
apply plugin: 'application'

mainClassName = 'org.apache.spark.jruby.REPLMain'
version '1.0-SNAPSHOT'

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

repositories {
    maven { url 'http://ci.jruby.org/snapshots/maven/'}
    mavenCentral()
}

sourceSets {
    main {
        scala {
            srcDirs = ['src/main/scala', 'src/main/java']
        }
        java {
            srcDirs = []
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile group: 'org.apache.spark', name: 'spark-core_2.10', version: '1.6.0'
    compile group: 'jline', name: 'jline', version: '2.11'
    compile files('libs/jruby-complete-9.1.0.0-SNAPSHOT.jar')
    //compile group
    // compile group: 'org.jruby', name: 'jruby-core', version: '9.0.4.0'
    // compile group: 'org.jruby', name: 'jruby-core', version: '9.1.0.0-SNAPSHOT'

}

build << {
    File script = file('start.sh')

    script.withPrintWriter {
        it.println '#!/bin/sh'
        it.println "java -cp ${getRuntimeClasspath()} org.jruby.JarBootstrapMain \"\$@\""
    }

    // make it executable
    ant.chmod(file: script.absolutePath, perm: 'u+x')
}

jar {
    manifest {
        attributes "Main-Class": 'org.apache.spark.jruby.REPLMain'
    }
}

String getRuntimeClasspath() {
    sourceSets.main.runtimeClasspath.collect { it.absolutePath }.join(':')
}
